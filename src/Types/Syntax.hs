module Types.Syntax
  ( Ty (..),
  )
where

import Test.QuickCheck

-- Contains ASTs for set-theoretic types that are convenient
-- when writing tests and/or programs but are not designed
-- for efficient computation (see LazyBDD.hs for that)

data Ty
  = T
  | F
  | Z
  | Prod Ty Ty
  | Arrow Ty Ty
  | Or [Ty]
  | And [Ty]
  | Not Ty
  | Any
  | Empty
  deriving (Eq, Show, Ord)

instance Arbitrary Ty where
  arbitrary = sized arbitrarySizedTy
  shrink = shrinkTy

arbitrarySizedTy :: Int -> Gen Ty
arbitrarySizedTy n
  | n < 2 =
      frequency
        [ (10, pure T),
          (10, pure F),
          (10, pure Z),
          (2, pure Any),
          (1, pure Empty)
        ]
  | otherwise = do
      t1 <- arbitrarySizedTy n'
      t2 <- arbitrarySizedTy n'
      t3 <- arbitrarySizedTy n'
      frequency
        [ (5, return T),
          (5, return F),
          (5, return Z),
          (15, return (Prod t1 t2)),
          (15, return (Arrow t1 t2)),
          (30, return (Or [t1, t2])),
          (30, return (Or [t1, t2, t3])),
          (10, return (And [t1, t2])),
          (10, return (And [t1, t2, t3])),
          (5, return (Not t1)),
          (1, pure Any),
          (1, pure Empty)
        ]
  where
    n' = n `div` 2

shrinkTy :: Ty -> [Ty]
shrinkTy (Prod l r) =
  shrink l
    ++ shrink r
    ++ [Prod l' r' | (l', r') <- shrink (l, r)]
shrinkTy (Arrow d r) =
  shrink d
    ++ shrink r
    ++ [Arrow d' r' | (d', r') <- shrink (d, r)]
shrinkTy (Or [t1, t2]) =
  shrink t1
    ++ shrink t2
    ++ [t1, t2]
    ++ [Or [t1', t2'] | (t1', t2') <- shrink (t1, t2)]
shrinkTy (Or [t1, t2, t3]) =
  shrink t1
    ++ shrink t2
    ++ shrink t3
    ++ [t1, t2, t3]
    ++ [Or [t1', t2'] | (t1', t2') <- shrink (t1, t2)]
    ++ [Or [t2', t3'] | (t2', t3') <- shrink (t2, t3)]
    ++ [Or [t1', t3'] | (t1', t3') <- shrink (t1, t3)]
shrinkTy (And [t1, t2]) =
  shrink t1
    ++ shrink t2
    ++ [t1, t2]
    ++ [And [t1', t2'] | (t1', t2') <- shrink (t1, t2)]
shrinkTy (And [t1, t2, t3]) =
  shrink t1
    ++ shrink t2
    ++ shrink t3
    ++ [t1, t2, t3]
    ++ [And [t1', t2'] | (t1', t2') <- shrink (t1, t2)]
    ++ [And [t2', t3'] | (t2', t3') <- shrink (t2, t3)]
    ++ [And [t1', t3'] | (t1', t3') <- shrink (t1, t3)]
shrinkTy (Not t) = [Not t' | t' <- shrink t]
shrinkTy _ = []
